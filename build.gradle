plugins {
    id 'java'
    id("com.gradleup.shadow") version "9.3.0"
}

group = 'com.jotrorox'
version = '0.4'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT"
    shadow "org.xerial:sqlite-jdbc:3.51.1.0"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}


configurations {
    shadow
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 21
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') { expand props }
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set('')

    mergeServiceFiles()
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'

    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}
